/*
Apply this plugin at the root level of your finagle scala service
to enable scrooge compilation for your *-api submodule.
*/

buildscript {
  repositories {
    mavenCentral()
    mavenLocal()
    //Allows us to download the adazza scrooge plugin.
    maven {
      url "s3://adazza-maven-repo/release"
      credentials(AwsCredentials) {
        accessKey System.env.AWS_ACCESS_KEY_ID
        secretKey System.env.AWS_SECRET_ACCESS_KEY
      }
    }
    maven {
      url "s3://adazza-maven-repo/snapshot"
      credentials(AwsCredentials) {
        accessKey System.env.AWS_ACCESS_KEY_ID
        secretKey System.env.AWS_SECRET_ACCESS_KEY
      }
    }
  }
  dependencies {
    // NOTE (daniel): Do I need to apply adazza-scala before hand if I need it in both buildscript and configure?
    classpath group: 'com.twitter', name: "scrooge-generator_$scalaVersion", version: "4.14.0"
    classpath group: 'com.adazza.gradle.scrooge', name: "scrooge-gradle-plugin", version: '0.3.0-dev.1.uncommitted+scrooge-scala-versions.836e634'
    //classpath group: 'com.adazza.gradle.scrooge', name: "scrooge-gradle-plugin", version: '0.2.0'
  }
}

configure(subprojects.findAll { it.name.endsWith('-api') }) {
  apply from: 'file:///Users/danielbarajas/adazza-repos/adazza-gradle/adazza-scala.gradle'
  apply plugin: com.adazza.gradle.scrooge.ScroogePlugin

  compileScrooge {
    thriftFiles = fileTree(dir: "${projectDir}/src/main/thrift/", include: "**/*.thrift")
    dest = file("${buildDir}/generated-sources/")
    opts = ["--finagle", "--verbose", "-s"]
  }

  sourceSets {
    main {
      scala {
        srcDir compileScrooge.dest
      }
      java {
        srcDir "${projectDir}/src/main/thrift/"
      }
    }
  }

  compileScala.dependsOn compileScrooge

  dependencies {
    compile "com.twitter:finagle-core_$scalaVersion"
    compile "com.twitter:finagle-thrift_$scalaVersion"
    compile "com.twitter:scrooge-core_$scalaVersion"
  }
}

