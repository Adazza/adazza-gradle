/*
Apply this plugin at the root level of your finagle scala service
to enable scrooge compilation for your *-api submodule.
*/
buildscript {
  repositories {
    mavenCentral()
    mavenLocal()
    //Allows us to download the adazza scrooge plugin.
    maven {
      url "s3://adazza-maven-repo/release"
      authentication {
          awsIm(AwsImAuthentication) // load from EC2 role or env var
      }
    }
    maven {
      url "s3://adazza-maven-repo/snapshot"
      authentication {
          awsIm(AwsImAuthentication) // load from EC2 role or env var
      }
    }
  }
  dependencies {
    classpath "com.twitter:scrooge-core_$scalaVersion:19.4.0"
    classpath "com.twitter:scrooge-generator_$scalaVersion:19.4.0"
    classpath group: 'com.adazza.gradle.scrooge', name: "scrooge-gradle-plugin_$scalaVersion", version: '19.4.0'
  }
}

configure(subprojects.findAll { it.name.endsWith('-api') }) {
  apply from: "https://raw.githubusercontent.com/adazza/adazza-gradle/${gradleBranch}/adazza-scala.gradle"
  apply plugin: com.adazza.gradle.scrooge.ScroogePlugin

  compileScrooge {
    thriftFiles = fileTree(dir: "${projectDir}/src/main/thrift/", include: "**/*.thrift")
    dest = file("${buildDir}/generated-sources/")
    opts = ["--finagle", "--verbose", "-s"]
  }

  sourceSets {
    main {
      scala {
        srcDir compileScrooge.dest
      }
      java {
        srcDir "${projectDir}/src/main/thrift/"
      }
    }
  }

  compileScala.dependsOn compileScrooge

  dependencies {
    compile "com.twitter:finagle-core_$scalaVersion"
    compile "com.twitter:finagle-thrift_$scalaVersion"
    compile "com.twitter:scrooge-core_$scalaVersion"
  }
}
